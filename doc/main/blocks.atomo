#title:{Blocks}tag:"blocks"

#define:{
    Block
    > Object
}body:{
    Blocks are the bread and butter of Atomo. They represent chunks of unevaluated code - sort of like lists of expressions, but with a reference to the context in which they were evaluated.

    They are first-class values and can be passed around, evaluated, torn apart, created programmatically, sent between processes, etc.

    #example-segment:{
        { "hi" print; @ok }
        { a | a + 2 }
        {}
    }
}

#define:{
    Block: expressions
    | expressions is-a?: List
    > Block
}body:{
    Calls #hl:{new:in:} with its sender as the context.

    #example:{
        orig = { a = 1; a + 1 }
        (Block: orig contents) call
    }
}

#define:{
    Block new: expressions in: context
    | expressions is-a?: List
    > Block
}body:{
    Returns a new #hl:{Block} with #hl:{expressions} as its contents and #hl:{context} as its context.

    #example:{
        orig = { a = 1; a + 1 }
        Block new: orig contents in: this
        adder = { * 7 }
        (Block new: adder contents in: 6) call
    }
}

#define:{
    b call
    | b is-a?: Block
    > any
}body:{
    Returns the result of evaluating each expression in #atomo:b, in order, with a new toplevel object that delegates to the block's context. #hl:{call}ing a block does not affect its context (unless it is a block returned by #atomo:in-context).

    Sending #hl:{call} to an empty block raises a #hl:{@no-expressions} error.

    #example:{
        a = 42
        { a = 1; a + 1 } call
        a
        {} call
    }
}

#define:{
    b call: l
    | b is-a?: Block
    | l is-a?: List
    > any
}body:{
    Like #atomo:call, but passes some extra arguments along to #atomo:b. Any number of arguments may be passed to the block; it can use however many it wants.

    Sending #hl:{call:} to an empty block raises a #hl:{@no-expressions} error.

    #example:{
        { a b | a + b } call: [1, 4]
        { a | a + 1 } call: [1]
        { 1 + 1 } call: [42]
        {} call: [42]
    }

    Note that the arguments also do not affect the outer scope:

    #example:{
        a = 42
        { a | a + 1 } call: [10]
        a
    }
}

#define:{
    v join: b
    | b is-a?: Block
    > any
}body:{
    Execute #atomo:b with #atomo:(v delegating-to: b context) as its toplevel object (i.e., the block's context is accessible). Returns the result of the block's execution.

    Note that #hl:{v}'s methods take priority over the block's context.

    #example:{
        True join: { not }
        1 join: { + 2 }
        x = 42
        1 join: { + x }
    }

    This can be useful for executing a block in the current scope:

    #example:{
        blk = { x = 1; x + a }
        Object clone do: { a = 42; res = join: blk }
    }
}

#define:{
    v join: b with: l
    | b is-a?: Block
    | l is-a?: List
    > any
}body:{
    Like #hl:{join:}, but with arguments passed to the block.

    Note that #hl:{v}'s methods take priority over both the arguments and the block's context.

    #example:{
        1 join: { a | + a } with: [2]
    }
}

#define:{
    v do: b
    | b is-a?: Block
    > v
}body:{
    Like #hl:{join:}, but returns #atomo:v. Handy for quick object creation.

    #example:{
        X = Object clone do: { a = 1; b = 2 }
    }
}

#define:{
    v do: b with: l
    | b is-a?: Block
    | l is-a?: List
    > v
}body:{
    Like #hl:{do:}, but with arguments passed to the block.

    #example:{
        X = Object clone do: { a | x = a; y := a + 1 } with: [1]
        X y
    }
}


#define:{
    b contents
    | b is-a?: Block
    > List
}body:{
    Returns a list of the expressions contained in #atomo:b.

    #example:{
        { a = 1; a + 1 } contents
    }
}

#define:{
    b context
    | b is-a?: Block
    > Object
}body:{
    Returns the context that the #atomo:b was evaluated in.

    #example:{
        a = 1
        {} context a
    }
}

#define:{
    b arguments
    | b is-a?: Block
    > List
}body:{
    Returns a list of the block's arguments, which are #hl:{Pattern}s.

    #example:{
        { a (x . _) 0 | a + x } arguments
    }
}

#define:{
    b in-context
    | b is-a?: Block
    > is-a?: Block
}body:{
    Returns an object that delegates to the original block, #atomo:b, but has its own #atomo:call and #hl:{call:} methods defined. These overrides evaluate the block in its original scope, as opposed to lexically, via #hl:{join:} and #hl:{join:with:}.

    #example:{
        a = 42
        { a = 1; a } call
        a
        { a = 1; a } in-context call
        a
    }
}

#define:{
    start to: end by: diff do: b
    | start is-a?: Integer
    | end is-a?: Integer
    | diff is-a?: Integer
    | b is-a?: Block
    > @ok
}body:{
    Calls #atomo:b with each integer in the range from #atomo:start to #atomo:end inclusive, by adding #atomo:diff to the current number.

    #example:{
        0 to: 5 by: 2 do: { n | ("Counting up... " .. n show) print }
        5 to: 0 by: -1 do: { n | ("Counting down... " .. n show) print }
    }
}

#define:{
    n times: b
    | n is-a?: Integer
    | b is-a?: Block
    > @ok
}body:{
    Alias for #atomo:(1 up-to: n do: b in-context).

    #example:{
        5 times: { "Odelay!" print }
    }
}

#define:{
    start up-to: end do: b
    | start is-a?: Integer
    | end is-a?: Integer
    | b is-a?: Block
    > @ok
}body:{
    Alias for #atomo:(start to: end by: 1 do: b).
}

#define:{
    start down-to: end do: b
    | start is-a?: Integer
    | end is-a?: Integer
    | b is-a?: Block
    > @ok
}body:{
    Alias for #atomo:(start to: end by: -1 do: b).
}

#define:{
    b repeat
    | b is-a?: Block
    > none
}body:{
    Repeatedly sends #atomo:call to #atomo:(b in-context). Never returns.
}
