module: Ordered: {
  succ := from-ordering: (ordering + 1)
  pred := from-ordering: (ordering - 1)

  shift: 0 := me
  shift: (n: Integer) :=
    if: (n < 0)
      then: { pred (shift: (n + 1)) }
      else: { succ (shift: (n - 1)) }

  to: x by: diff :=
    condition: {
      diff == 0 ->
        error: @zero-diff

      ((diff < 0 && ordering < x ordering) ||
        diff > 0 && ordering > x ordering) ->
        []

      otherwise ->
        me . ((shift: diff) to: x by: diff)
    }

  up-to: x := me to: x by: 1

  down-to: x := me to: x by: -1
}

class: Integer: {
  include: Ordered

  ordering := me
  from-ordering: n := n
}

class: Char: {
  include: Ordered

  ordering := ord
  from-ordering: n := n chr
}

class: Double: {
  include: Ordered

  ordering := as: Integer
  from-ordering: n := n as: Double

  shift: (n: Double) := + n
}

class: Rational: {
  include: Ordered

  ordering := as: Integer
  from-ordering: n := n as: Rational

  shift: (n: Rational) := + n
}
